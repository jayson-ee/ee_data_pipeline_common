config {
    type: "table",
    dependencies: ["test_events_ga4_stg05"]
}

with product_events as(
select
event_datetime_ct
,event_timestamp
,event_name
,unique_session_id
,user_pseudo_id
,items.item_name
,items.item_id variant_id
,first_value (marketing_channel ignore nulls) over (partition by unique_session_id order by event_timestamp asc) marketing_channel
,first_value (medium ignore nulls) over (partition by unique_session_id order by event_timestamp asc) medium
,first_value (campaign ignore nulls) over (partition by unique_session_id order by event_timestamp asc) campaign
,first_value (device ignore nulls) over (partition by unique_session_id order by event_timestamp asc) device
,first_value (device_brand ignore nulls) over (partition by unique_session_id order by event_timestamp asc) device_brand
,first_value (device_model ignore nulls) over (partition by unique_session_id order by event_timestamp asc) device_model
,first_value (operating_system ignore nulls) over (partition by unique_session_id order by event_timestamp asc) operating_system
,first_value (operating_system_version ignore nulls) over (partition by unique_session_id order by event_timestamp asc) operating_system_version
,first_value (browser ignore nulls) over (partition by unique_session_id order by event_timestamp asc) browser
,first_value (browser_version ignore nulls) over (partition by unique_session_id order by event_timestamp asc) browser_version
from ${ref("events_ga4_stg05_with_campaign_name_from_gclid")}
left join unnest (items_arr) items
where lower(event_name) in ('view_item','remove_from_cart', 'add_to_cart')
)

, variant_names as (
select 
date(event_datetime_ct) date
,variant_id 
,event_name
,user_pseudo_id
,variant_names.orderline_name
,marketing_channel
,medium
,campaign
,device
,device_brand
,device_model
,operating_system
,operating_system_version
,browser
,browser_version
from product_events 
left join ${ref("mapping_shopify_variant_id")} variant_names
on product_events.variant_id = variant_names.orderline_variant_id and date(event_datetime_ct) between variant_names.start_date and variant_names.end_date
)


select 
date
,user_pseudo_id
,variant_id orderline_variant_id
,orderline_name 
,marketing_channel
,medium
,campaign
,device
,device_brand
,device_model
,operating_system
,operating_system_version
,browser
,browser_version
,countif (event_name = 'view_item') product_views
,countif (event_name = 'add_to_cart') product_added_to_cart
,countif (event_name = 'remove_from_cart') product_removed_from_cart
from variant_names
where orderline_name is not null
group by 
date
,user_pseudo_id
,orderline_variant_id
,orderline_name 
,marketing_channel
,medium
,campaign
,device
,device_brand
,device_model
,operating_system
,operating_system_version
,browser
,browser_version